/*
 * The Common.lsd script contains a collection of helper functions.  
 *
 * the getTitle() closure is specific to the MASC datasources, but the rest
 * are general purpose functions.
 */
 
import groovy.xml.*
include 'Servers'

// The header source is used by the getTitle closure to retrieve the GrAF header
// for a document.
def headersource = Datasource {
	server servers.vassar
	name 'anc:masc.headers_1.3.0'
}

// The XmlParser is used in getTitle to parse the GrAF header files fetched
// from the headersource
def parser = new XmlParser()


/*
 * Fetches the document header for the given document and retrieves the title.
 *
 * @params
 * key - the MASC document ID value as given by the @docId attribute in the
 *       GrAF header file.
 */
getTitle = { key ->
	def data = headersource.get(key)
	def header = parser.parseText(data.payload)
	try {
		return header.fileDesc.sourceDesc.title.text()
	}
	catch (Exception e) {
		return key
	}
}

/*
 * Returns the discriminator name for the discriminator in the Data
 * object.
 */
getType = { data ->
	return DiscriminatorRegistry.get(data.discriminator)
}

/*
 * Creates a new directory at the given path or throws an IOException.
 */
createDir = { path ->
	File file = new File(path)
	if (!file.exists()) {
		if (!file.mkdirs()) {
			throw new IOException("Unable to create ${path}")
		}
	}
	return file
}

/*
 * Prints the discriminator names for all values in a list of
 * discriminator values.
 */
printTypes = { types ->
	def names = []
	types.each { type ->
		//println "\t(${type}) ${DiscriminatorRegistry.get(type)}"
		names << DiscriminatorRegistry.get(type)
	}
	return names.join(", ")
}

/*
 * Pretty prints the data in a container object.
 */
prettyPrint = { data ->
	println new Container(data.payload).toPrettyJson()
}

/*
 * Prints out the requirements and annotations produced by a given
 * service.
 */
producesRequires = { service ->
	println "\tRequires: ${printTypes(service.requires())}"	
	println "\tProduces: ${printTypes(service.produces())}"
}

/*
 * Checks that a data object is not null, does not contain and error code, and
 * (optionally) that the discriminator is of the given type.
 */
check = { data, type=null ->
	if (data == null) {
		throw new NullPointerException("Data object is null.")
	}
	if (data.discriminator == Types.ERROR) {
		throw new LappsException('ERROR: ' + data.payload)
	}
	
	if (type && data.discriminator != type) {
		def expected = DiscriminatorRegistry.get(type)
		def found = DiscriminatorRegistry.get(data.discriminator)
		throw new LappsException("Invalid discriminator type. Expected:${expected} Found:${found}")
	}
		
}

/*
 * Checks that each of the values present in requires is also present
 * in actual.
 */
checkArgs = { actual, required ->
	boolean result = true
	required.each { name ->
		if (actual[name] == null) {
			println "Missing parameter: ${name}"
			result = false
		}
	}
	return result
}