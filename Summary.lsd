/*
 * Summarizes the annotations in a directory of files.
 */
//include 'LAPPS'
include 'Tomcat'

ext = 'json'
['txt', 'xml', 'csv', 'json'].each {
	if (args[it]) {
		ext = it
	}
}

if (!args.path) {
    println "No input path specified."
    return
}

def version = args.v ?: args.ver ?: args.version ?: '0.3.1-SNAPSHOT'
//def node = Nodes.getNode()
summarizer = new Summarizer()

//convert = Service {
//	server node.server
//	name "${node.id}:convert.gate2json_${version}"
//}

convert = getConverters('http://localhost:8080', version)

void scan(File directory) {
	println "Scanning ${directory.path} for ${ext} files"
	directory.eachFileMatch(~/.*\.${ext}$/) { file ->
		try {
		    process(file)
		}
		catch (Exception e) {
		    println "Unable to process ${file.path} : ${e.message}"
		}
	}
}

void process(File file) {
	println "Processing ${file.path}"
	Container container
	if (file.name.endsWith(".xml")) {
		Data data = new Data(Types.GATE, file.text)
		data = convert.gate2json.execute(data)
		container = new Container(data.payload)
	}
	else {
		container = new Container(file.text)
	}
	summarizer.summarize(container)
}

File directory = new File(args.path)
if (!directory.exists()) {
	println "Directory not found: ${directory.path}"
	return
}

if (directory.isDirectory()) {
	directory.eachDirRecurse { dir->
		scan(dir)
	}	
	scan(directory)
}
else {
	process(directory)
}
println()
summarizer.print()

return

class Summarizer {
	def annotations = [:]
	def providers = new HashSet()
	void summarize(Container container) {
		container.steps.each { ProcessingStep step ->
			def provider = step.metadata.producedBy
			if (provider) {
				provider.split(", ").each { 				
					providers << it
				}
			}
			step.annotations.each { a ->
				AnnotationInfo info = annotations[a.label]
				if (info == null) {
					info = new AnnotationInfo(label:a.label)
					annotations[a.label] = info
				}
				info.increment()
				a.features.each { name,value ->
					info.addFeature(name)
				}
			}
		}
	}
	
	void print() {
		println "Annotation Providers:"
		providers.each { println it }
		println()
		
		println "Annotations"
		annotations.each { name,info ->
			info.print()
		}
		println()
	}
}

class AnnotationInfo {
	String label
	int count = 0
	Map features = [:]
	
	void increment() {
		++count
	}
	
	void addFeature(String name) {
		Counter count = features[name]
		if (count == null) {
			count = new Counter()
			features[name] = count
		}
		count.increment()
	}
	
	void print() {
		println "${label} : ${count}"
		features.each { name, count ->
			println "\t${name} : ${count}"
		}
	}
}
